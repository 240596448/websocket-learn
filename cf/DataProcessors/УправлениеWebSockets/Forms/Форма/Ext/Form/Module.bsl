
&НаКлиенте
Процедура Подключить(Команда)
	Команда1НаСервере();
КонецПроцедуры

&НаСервере
Процедура Команда1НаСервере()
	Соединение = Клиент().Подключить();
	Ключ = Соединение.Ключ;
КонецПроцедуры

&НаКлиенте
Процедура СообщениеПриИзменении(Элемент)
	ОтправитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОтправитьНаСервере()
	Соединение = Соединение(Ключ);
	Если Соединение = Неопределено Тогда
		Сообщить("Соединение не установлено");
		Возврат;
	КонецЕсли;
	Соединение.ОтправитьСообщение(Сообщение);
КонецПроцедуры

&НаСервереБезКонтекста
Функция Клиент()
	
	Возврат WebSocketКлиенты.НайтиПредопределенный(Метаданные.WebSocketКлиенты.WebSocketКлиент1);

КонецФункции

&НаСервереБезКонтекста
Функция Соединение(Ключ=Неопределено)
	Если Ключ=Неопределено Тогда
		Возврат WebSocketКлиентСоединения.ПолучитьСоединения();
	Иначе
		Возврат WebSocketКлиентСоединения.ПолучитьСоединение(Ключ);
	КонецЕсли;
КонецФункции

&НаСервере
Процедура ОтключитьНаСервере()
	Клиент().Отключить();
КонецПроцедуры

&НаКлиенте
Процедура Отключить(Команда)
	ОтключитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УведомленияКлиента.ПодключитьОбработчик("ws",
		Новый ОписаниеОповещения("НовоеСообщение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура НовоеСообщение(Сообщение, ДопПараметры) Экспорт
	ДобавитьСтроку(Ответы, Сообщение);
	ЭтаФорма.ТекущийЭлемент = Элементы.Сообщение;
КонецПроцедуры

&НаСервере
Процедура ЗамерНаСервере()
	Сообщение = "__BENCHMARK_DATA__Тестовое сообщение для замера производительности";
	Соединение = Соединение(Ключ);
	Если Соединение = Неопределено Тогда
		Сообщить("Соединение не установлено");
		Возврат;
	КонецЕсли;
	Соединение.ОтправитьСообщение("__BENCHMARK_START__");
	Начало = ТекущаяУниверсальнаяДатаВМиллисекундах();
	сч = 0;
	Пока Начало + 15*1000 > ТекущаяУниверсальнаяДатаВМиллисекундах() 
	//	И сч < 100 
	Цикл
		Соединение.ОтправитьСообщение(Сообщение);
		сч = сч + 1;
	КонецЦикла;
	Конец = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Соединение.ОтправитьСообщение("__BENCHMARK_END__");
	
	Сек = (Конец-Начало)/1000;
	ДобавитьСтроку(Ответы, СтрШаблон("Отправлено %1 сообщений за %2 сек. %3 сообщений/секунду",
		сч, Сек, ?(сек=0,0,Цел(сч/Сек))));
КонецПроцедуры

&НаКлиенте
Процедура Замер(Команда)
	ЗамерНаСервере();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьСтроку(Ответы, Сообщение)
	Ответы.ДобавитьСтроку(СтрШаблон("%1 %2",
		ТекущаяДата(), Сообщение));
КонецПроцедуры

&НаСервере
Процедура ЗамерВходящийНаСервере()
	Соединение = Соединение(Ключ);
	Если Соединение = Неопределено Тогда
		Сообщить("Соединение не установлено");
		Возврат;
	КонецЕсли;
	Соединение.ОтправитьСообщение("__BENCHMARK_START__:"+Формат(Количество, "ЧГ="));
КонецПроцедуры

&НаКлиенте
Процедура ЗамерВходящий(Команда)
	ЗамерВходящийНаСервере();
КонецПроцедуры
